plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version "1.1.16"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "Parchment"
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = "Porting Lib"
		url = "https://mvn.devos.one/releases/"
	}
	maven {
		name = "CCA"
		url = 'https://maven.ladysnake.org/releases'
	}
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://jitpack.io/" }
	maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url 'https://maven.blamejared.com' }
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://cursemaven.com" }
	maven { url "https://maven.bai.lol" }
	maven { url 'https://maven.kosmx.dev/' }
	maven { url 'https://libs.azuredoom.com:4443/mods'}
	maven { url "https://maven.parchmentmc.org" }
	mavenLocal()
}

dependencies {
	// Base
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		// Use parchment mappings. NOTE: Parchment maven must be manually added. (https://maven.parchmentmc.org)
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.06.26@zip")
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"


	// Zenith
	modImplementation "maven.modrinth:zenith-attributes:${project.zenith_attributes_version}"
	//modImplementation "maven.modrinth:zenith:${project.zenith_version}"
	modImplementation "maven.modrinth:zenith:${project.zenith_version}"
	modImplementation "maven.modrinth:fakerlib:${project.fakerlib_version}"
	// RPG Series
	modImplementation("maven.modrinth:projectile-damage-attribute:${project.projectile_damage}-fabric")
	modImplementation("maven.modrinth:spell-power:${project.spell_power_version}-fabric")
	modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}-fabric")
	modImplementation("maven.modrinth:wizards:${project.wizards_version}-fabric")
	modImplementation("maven.modrinth:paladins-and-priests:${project.paladins_version}-fabric")
	modImplementation("maven.modrinth:archers:${project.archers_version}-fabric")
	modImplementation("maven.modrinth:runes:${project.runes_version}-fabric")
	modImplementation("maven.modrinth:better-combat:${project.bettercombat_version}-fabric")
	modImplementation("maven.modrinth:jewelry:${project.jewelry_version}-fabric")
	modImplementation("maven.modrinth:structure-pool-api:${project.structure_pool_api_version}-fabric")

	modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim_version}")
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}")
	modImplementation("dev.emi:trinkets:${project.trinkets_version}")
	// I want to die
	implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0-beta.10"))
	implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.0.2"))
	modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.rea_version}")
	modImplementation("maven.modrinth:AdditionalEntityAttributes:${project.aea_version}")
	modImplementation("io.github.fabricators_of_create.Porting-Lib:loot:${project.port_lib_version}") //doubt
	modImplementation("io.github.fabricators_of_create.Porting-Lib:base:${project.port_lib_version}")
	modImplementation("io.github.fabricators_of_create.Porting-Lib:tags:${project.port_lib_version}")
	modImplementation("io.github.fabricators_of_create.Porting-Lib:utility:${project.port_lib_version}")
	modImplementation "curse.maven:jade-324717:${jade_id}"
	modImplementation("com.github.Chocohead:Fabric-ASM:${project.fabric_asm_version}")
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
	modImplementation("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}")
	implementation("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}")
	modImplementation("mod.azure.azurelibarmor:azurelibarmor-fabric-${project.azure_mc_version}:${project.azure_lib_version}")

}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "zephyr-mod"
	versionType = "release"
	versionName = "Zephyr " + project.version
	uploadFile = remapJar
	gameVersions = ["1.20.1"]
	loaders = ["fabric"]
	changelog = "[Changelog](https://github.com/muon-rw/Zephyr/blob/1.20/changelog.md)"
	//debugMode = true
	dependencies {
		required.project("zenith")
		required.project("spell-power")
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("CF_TOKEN")
	//debugMode = true
	def mainFile = upload(924809, remapJar)
	mainFile.changelog = "https://github.com/muon-rw/Zephyr/blob/1.20/changelog.md"
	mainFile.releaseType = "release"
	mainFile.addGameVersion("1.20.1")
	mainFile.addModLoader("fabric")
	mainFile.displayName = project.archives_base_name + "-" + project.version
	mainFile.addRequirement("zenith")
	mainFile.addRequirement("spell-power")
	mainFile.addOptional("better-combat-by-daedelus")
	mainFile.addOptional("paladins-and-priests")
	mainFile.addOptional("wizards")
	mainFile.addOptional("jewelry")
}